import "@cadl-lang/rest";
import "@cadl-lang/versioning";
import "@azure-tools/cadl-providerhub";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-azure-resource-manager";

using Cadl.Http;
using Cadl.Rest;
using Cadl.Versioning;
using Azure.Core;
using Azure.ResourceManager;

@armProviderNamespace
@serviceTitle("Encrypted Transport Client")
@serviceVersion("2022-06-17-preview")
@doc("Encrypted Transport Management API")
@versionedDependency(Azure.ResourceManager.Versions.v1_0_Preview_1)
namespace Microsoft.EncryptedTransport;

@doc("An Encrypted Transport Resource")
model EncryptedTransportResource is TrackedResource<EncryptedTransportResourceProperties> {
  @doc("Name of EncryptedTransport")
  @pattern("^[a-zA-Z0-9]{3,24}$")
  @key("encryptedTransportName")
  @path
  @segment("encryptedTransport")
  name: string;

}

@doc("Encrypted Transport Resource resource properties")
model EncryptedTransportResourceProperties {
  @doc("The Encrypted Transport's display name")
  displayName: string;

  @doc("The Encrypted Transport region's display name")
  regionDisplayName: string;

  @doc("The type location")
  regionType: string;

  @doc("The category of the location")
  regionCategory: string;

  @doc("The geography group of the location")
  geographyGroup: string;

  @visibility("read")
  @doc("The status of the last operation.")
  provisioningState?: ProvisioningState;
}

@doc("An End User Device Resource ")
@parentResource(EncryptedTransportResource)
model EndUserDeviceResource is TrackedResource<EndUserDeviceResourceProperties> {
  @doc("EndUserDevice name")
  @pattern("^[a-zA-Z0-9]{3,24}$")
  @key("endUserDeviceName")
  @segment("endUserDevices")
  @path
  name: string;
}

@doc("An End User Device object belonging to an Encrypted Transport.")
model EndUserDeviceResourceProperties {
  // Standard MAC address format
  // 00:00:00:00:00:00
  // lowercase, uppercase letters, 6 groups of 2 hex digits, separated by colons
  @doc("The MAC address.")
  @pattern("^[a-fA-F0-9]{2}(:[a-fA-F0-9]{2}){5}$")
  macAddress: string;

  // Standard IPv4 address format
  // 255.255.255.255
  // 4 groups of 1-3 digits, separated by periods
  @doc("The ipv4 address.")
  @pattern("^(25[0-5]|2[0-4][0-9]|[01]?[0-9][0-9]?)([.](25[0-5]|2[0-4][0-9]|[01]?[0-9][0-9]?)){3}$")
  ipv4Address: string;

  // Standard IPv6 address format
  // 1:2:3:4:5:6:7:8
  // 1::                              1:2:3:4:5:6:7::
  // 1::8             1:2:3:4:5:6::8  1:2:3:4:5:6::8
  // 1::7:8           1:2:3:4:5::7:8  1:2:3:4:5::8
  // 1::6:7:8         1:2:3:4::6:7:8  1:2:3:4::8
  // 1::5:6:7:8       1:2:3::5:6:7:8  1:2:3::8
  // 1::4:5:6:7:8     1:2::4:5:6:7:8  1:2::8
  // 1::3:4:5:6:7:8   1::3:4:5:6:7:8  1::8  
  // ::2:3:4:5:6:7:8  ::2:3:4:5:6:7:8 ::8       ::     
  // fe80::7:8%eth0   fe80::7:8%1     (link-local IPv6 addresses with zone index)
  // ::255.255.255.255   ::ffff:255.255.255.255  ::ffff:0:255.255.255.255  (IPv4-mapped IPv6 addresses and IPv4-translated addresses)
  // 2001:db8:3:4::192.0.2.33  64:ff9b::192.0.2.33 (IPv4-Embedded IPv6 Address)
  // look ahead and behind for whitespace

  @doc("The ipv6 address.")
  @pattern("^(([0-9a-fA-F]{1,4}:){7,7}[0-9a-fA-F]{1,4}|([0-9a-fA-F]{1,4}:){1,7}:|([0-9a-fA-F]{1,4}:){1,6}:[0-9a-fA-F]{1,4}|([0-9a-fA-F]{1,4}:){1,5}(:[0-9a-fA-F]{1,4}){1,2}|([0-9a-fA-F]{1,4}:){1,4}(:[0-9a-fA-F]{1,4}){1,3}|([0-9a-fA-F]{1,4}:){1,3}(:[0-9a-fA-F]{1,4}){1,4}|([0-9a-fA-F]{1,4}:){1,2}(:[0-9a-fA-F]{1,4}){1,5}|[0-9a-fA-F]{1,4}:((:[0-9a-fA-F]{1,4}){1,6})|:((:[0-9a-fA-F]{1,4}){1,7}|:)|fe80:(:[0-9a-fA-F]{0,4}){0,4}%[0-9a-zA-Z]{1,}|::(ffff(:0{1,4}){0,1}:){0,1}((25[0-5]|(2[0-4]|1{0,1}[0-9]){0,1}[0-9])\\.){3,3}(25[0-5]|(2[0-4]|1{0,1}[0-9]){0,1}[0-9])|([0-9a-fA-F]{1,4}:){1,4}:((25[0-5]|(2[0-4]|1{0,1}[0-9]){0,1}[0-9])\\.){3,3}(25[0-5]|(2[0-4]|1{0,1}[0-9]){0,1}[0-9]))$")
  ipv6Address: string;

  // Uppercase letters and numbers only, min 3, max 9 characters
  @doc("The Owner ID.")
  @pattern("^[A-Z0-9]{3,9}$")
  ownerId: string;

  @visibility("read")
  @doc("The status of the last operation.")
  provisioningState?: ProvisioningState;

}

@armResourceOperations
interface EncryptedTransport extends ResourceOperations<EncryptedTransportResource,EncryptedTransportResourceProperties> {}

@armResourceOperations
interface EndUserDevices extends ResourceOperations<EndUserDeviceResource, EndUserDeviceResourceProperties> {}

@knownValues(ProvisioningStateKV)
@doc("Provisioning State")
model ProvisioningState is string {}

@lroStatus
enum ProvisioningStateKV {
  ...ResourceProvisioningStateKV,
  Provisioning,
  Updating,
  Deleting,
  Accepted
}
