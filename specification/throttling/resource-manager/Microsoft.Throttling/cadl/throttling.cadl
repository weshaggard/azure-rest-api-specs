import "@cadl-lang/rest";
import "@cadl-lang/versioning";
import "@azure-tools/cadl-autorest";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-azure-resource-manager";
import "@azure-tools/cadl-providerhub";

@doc("Azure Throttling Solutions (ATS)")
@serviceTitle("throttling")
@serviceVersion("2022-06-01-preview")
@consumes("application/json")
@produces("application/json")
@armNamespace
@versionedDependency(Azure.ResourceManager.Versions.v1_0_Preview_1)
namespace Microsoft.Throttling;

using Cadl.Http;
using Cadl.Rest;
using Cadl.Versioning;
using Autorest;
using Azure.ResourceManager;
using OpenAPI;

@doc("Throttling account")
model Account is TrackedResource<AccountProperties> {
  @doc("Account name")
  @key("accountName")
  @segment("accounts")
  @visibility("read")
  @path
  name: string;
}

@armResourceOperations(Account)
interface Accounts extends ResourceOperations<Account, AccountProperties> {}

@doc("Provisioning state")
@knownValues(ProvisioningStateKV)
model ProvisioningState is string {}
enum ProvisioningStateKV {
  Succeeded,
  Failed,
  Canceled,
  Accepted,
  Creating,
  Updating,
  Deleting
}

@doc("Account properties")
model AccountProperties {
  @doc("Provisioning state")
  @visibility("read")
  provisioningState?: ProvisioningState;

  @doc("Endpoint address to be used by Throttling client")
  @visibility("read")
  hostName?: string;

  @doc("Account authentication properties")
  authentication: AuthenticationProperties;
}

@doc("Account authentication properties")
model AuthenticationProperties {
  @doc("Certificate-based authentication properties")
  @extension("x-ms-identifiers", [ "subjectName" ])
  certificates?: CertificateProperties[];

  @doc("AzureAD-based authentication properties")
  azureActiveDirectory?: AzureActiveDirectoryProperties;
}

@doc("Certificate-based authentication properties")
model CertificateProperties {
  @doc("Certificate subject name")
  subjectName: string;
}

@doc("AzureAD-based authentication properties")
model AzureActiveDirectoryProperties {
  @doc("Whether AzureAD-based authentication is enabled")
  enabled: boolean;

  @doc("List of permitted user-assigned managed identities")
  userAssignedIdentities: ResourceIdentifier[]
}

// TODO: use the built-in once shipped, see https://github.com/Azure/cadl-azure/issues/1810
@doc("User-assigned managed identity ARM resource id")
@format("arm-id")
model ResourceIdentifier is string {}

@doc("Throttling category")
@parentResource(Account)
model Category is ProxyResource<CategoryProperties> {
  @doc("Category name")
  @key("categoryName")
  @segment("categories")
  @visibility("read")
  @path
  name: string;
}

@armResourceOperations(Category)
interface Categories extends ResourceOperations<Category, CategoryProperties> {}

@doc("Category properties")
model CategoryProperties {
  @doc("Window duration")
  windowDuration: duration;
}

@doc("Throttling category")
@parentResource(Category)
model FixedThreshold is ProxyResource<FixedThresholdProperties> {
  @doc("Throttling algorithm Fixed threshold")
  @key("fixedThresholdName")
  @segment("fixedThresholds")
  @visibility("read")
  @path
  name: string;
}

@armResourceOperations(FixedThreshold)
interface FixedThresholds extends ResourceOperations<FixedThreshold, FixedThresholdProperties> {}

@doc("Fixed threshold properties")
model FixedThresholdProperties {
  @doc("Value of the limit")
  value: int32;

  @doc("Depth of the bucket")
  @minValue(0)
  @maxValue(16)
  depth: int32; // TODO: use int8
  
  @doc("Minimal decision duration")
  minDecisionDuration: duration;
}
