import "@cadl-lang/rest";
import "@cadl-lang/versioning";
import "@azure-tools/cadl-autorest";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-azure-resource-manager";
import "@azure-tools/cadl-providerhub";

@doc("Azure Throttling Solutions (ATS)")
@service({
  title: "Azure Throttling"
})
@versioned(Versions)
@armProviderNamespace
@versionedDependency(
  [
    [
      Versions.v2022_06_01_preview,
      Azure.ResourceManager.Versions.v1_0_Preview_1
    ]
  ]
)
namespace Microsoft.Throttling;

enum Versions {
  v2022_06_01_preview: "2022-06-01-preview",
}

using Cadl.Http;
using Cadl.Rest;
using Cadl.Versioning;
using Autorest;
using Azure.Core;
using Azure.ResourceManager;
using Azure.ResourceManager.Internal;
using OpenAPI;

interface Operations extends Azure.ResourceManager.Operations {}

@doc("Provisioning state")
@knownValues(ProvisioningStateKV)
model ProvisioningState is string;

@lroStatus
enum ProvisioningStateKV {
  ...ResourceProvisioningStateKV,
  Creating,
  Updating,
  Deleting
}

@doc("Throttling account")
model Account is TrackedResource<AccountProperties> {
  @doc("Account name")
  @key("accountName")
  @segment("accounts")
  @visibility("read")
  @path
  name: string;
}

@armResourceOperations(Account)
interface Accounts extends ResourceOperations<Account, AccountProperties> {}

@doc("Account properties")
model AccountProperties {
  @doc("Provisioning state")
  @visibility("read")
  provisioningState?: ProvisioningState;

  @doc("Endpoint address to be used by Throttling client")
  @visibility("read")
  hostName?: string;

  @doc("Account authentication properties")
  authentication: AuthenticationProperties;
}

@doc("Account authentication properties")
model AuthenticationProperties {
  @doc("Certificate-based authentication properties")
  @extension("x-ms-identifiers", [ "subjectName" ])
  certificates?: CertificateProperties[];

  @doc("AzureAD-based authentication properties")
  azureActiveDirectory?: AzureActiveDirectoryProperties;
}

@doc("Certificate-based authentication properties")
model CertificateProperties {
  @doc("Certificate subject name")
  subjectName: string;
}

@doc("AzureAD-based authentication properties")
model AzureActiveDirectoryProperties {
  @doc("List of permitted user-assigned managed identities")
  @minItems(1)
  userAssignedIdentities: UserAssignedManagedIdentityReference[]
}

@doc("User-assigned managed identity ARM resource id")
model UserAssignedManagedIdentityReference
  is ResourceIdentifier<[
    {
      type: "Microsoft.ManagedIdentity/userAssignedIdentities";
    }
  ]>;

@doc("Throttling category")
@parentResource(Account)
model Category is ProxyResource<CategoryProperties> {
  @doc("Category name")
  @key("categoryName")
  @segment("categories")
  @visibility("read")
  @path
  name: string;
}

@armResourceOperations(Category)
interface Categories extends ResourceOperations<Category, CategoryProperties> {}

@doc("Category properties")
model CategoryProperties {
  @doc("Provisioning state")
  @visibility("read")
  provisioningState?: ProvisioningState;

  @doc("Window duration")
  windowDuration: duration;
}

@doc("Throttling category")
@parentResource(Category)
model Threshold is ProxyResource<ThresholdProperties> {
  @doc("Throttling threshold")
  @key("thresholdName")
  @segment("thresholds")
  @visibility("read")
  @path
  name: string;
}

@armResourceOperations(Threshold)
interface Thresholds extends ResourceOperations<Threshold, ThresholdProperties> {}

@doc("Threshold properties")
model ThresholdProperties {
  @doc("Provisioning state")
  @visibility("read")
  provisioningState?: ProvisioningState;

  @doc("Threshold decision type, arbitrary string to be returned to the client")
  @maxLength(128)
  decisionType: string;

  @doc("Value of the limit")
  value: int32;

  @doc("Depth of the bucket")
  @minValue(0)
  @maxValue(16)
  depth: int32; // TODO: use int8
  
  @doc("Minimal decision duration")
  minDecisionDuration: duration;
}
