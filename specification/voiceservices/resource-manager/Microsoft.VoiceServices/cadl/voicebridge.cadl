// Definitions for the VoiceBridge resource and child resources

import "@cadl-lang/openapi";
import "@cadl-lang/rest";
import "@azure-tools/cadl-providerhub";
import "@azure-tools/cadl-autorest";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-azure-resource-manager";

namespace Microsoft.VoiceServices;

using Cadl.Http;
using Cadl.Rest;
using Azure.ResourceManager;

@doc("A TestLine resource")
@parentResource(VoiceBridge)
model TestLine is TrackedResource<TestLineProperties> {
  @pattern("^[a-zA-Z0-9-]{3,24}$")
  @key("testLineName")
  @segment("testLines")
  @doc("Unique identifier for this test line")
  @visibility("Read")
  @path
  name: string;
}

@doc("The purpose of the TestLine resource.")
enum TestLinePurpose {
  Manual,
  Automated,
}

@doc("Details of the TestLine resource.")
model TestLineProperties is ResourceProperties {
  @doc("The phone number")
  @visibility("read", "write", "create")
  number: string;
  @doc("Purpose of this test line, e.g. automated or manual testing")
  @visibility("read", "write", "create")
  purpose: TestLinePurpose;
}

@armResourceOperations
interface TestLines extends ResourceOperations<TestLine, TestLineProperties> {}

@doc("A Contact resource")
@parentResource(VoiceBridge)
model Contact is TrackedResource<ContactProperties> {
  @pattern("^[a-zA-Z0-9-]{3,24}$")
  @key("contactName")
  @segment("contacts")
  @doc("Unique identifier for this contact")
  @visibility("Read")
  @path
  name: string;
}

@doc("Details of the Contact resource.")
model ContactProperties is ResourceProperties {
  @doc("Full name of contact")
  @visibility("read", "write", "create")
  contactName: string;
  @doc("Telephone number of contact")
  @visibility("read", "write", "create")
  phoneNumber: string;
  @doc("Email address of contact")
  @visibility("read", "write", "create")
  email: string;
  @doc("Job title of contact")
  @visibility("read", "write", "create")
  role: string;
}

@armResourceOperations
interface Contacts extends ResourceOperations<Contact, ContactProperties> {}

@doc("A VoiceBridge resource")
model VoiceBridge is TrackedResource<VoiceBridgeProperties> {
  @pattern("^[a-zA-Z0-9-]{3,24}$")
  @key("voiceBridgeName")
  @segment("voiceBridges")
  @doc("Unique identifier for this deployment")
  @visibility("Read")
  @path
  name: string;
}

@doc("The status of the current VoiceBridge resource.")
enum Status {
  New,
  ChangePending,
  Complete,
}

@doc("The status of the current VoiceBridge resource.")
enum E911Type {
  None,
  Esrp,
  Elin
}

@doc("The voice codecs expected for communication with Teams.")
enum TeamsCodecs {
	PCMA,
	PCMU,
	G722,
	SILK_8,
	SILK_16
}

@doc("How this deployment connects back to the operator network, e.g. MAPS.")
enum Connectivity {
  Maps
}

@doc("The service region configuration needed for Teams Callings.")
model ServiceRegionProperties {
	@doc("The name of the region in which the resources needed for Teams Calling will be deployed.")
	@visibility("read", "create")
	name: string;
	@doc("IP address to use to contact the operator network from this region")
	@visibility("read", "write", "create")
	operatorAddresses: string[];
	@doc("IP address to use to contact the ESRPs from this region")
	@visibility("read", "write", "create")
	esrpAddresses?: string[];
}

@doc("Details of the VoiceBridge resource.")
model VoiceBridgeProperties is ResourceProperties {
  @doc("The current status of the deployment.")
  @visibility("read") status?: Status;

  @doc("The regions in which to deploy the resources needed for Teams Calling")
  @OpenAPI.extension("x-ms-identifiers", ["name"])
  @visibility("read", "write", "create")
  serviceLocations: ServiceRegionProperties[];

  @doc("How to connect back to the operator network, e.g. MAPS")
  @visibility("read", "create")
  connectivity: Connectivity;

  @doc("Voice codecs to support")
  @visibility("read", "write", "create")
  codecs: TeamsCodecs[];
  
  @doc("How to handle 911 calls")
  @visibility("read", "write", "create")
  e911Type: E911Type;
}

@armResourceOperations
interface VoiceBridges extends ResourceOperations<VoiceBridge, VoiceBridgeProperties> {}