import "@cadl-lang/rest";
import "@cadl-lang/versioning";
import "@azure-tools/cadl-autorest";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-azure-resource-manager";
import "@azure-tools/cadl-providerhub";
import "./models/commandrunner-3.cadl";
import "./models/deviceinfo-5.cadl";
import "./models/firewall-4.cadl";
import "./models/hostname-2.cadl";

using Cadl.Http;
using Cadl.Rest;
using Cadl.Versioning;
using OpenAPI;
using Autorest;
using Azure.Core;
using Azure.ResourceManager;
using Azure.ResourceManager.Foundations;

#suppress "@azure-tools/cadl-azure-core/casing-style" "Existing service name."
@armProviderNamespace
@service({
  title: "Microsoft.OSConfigPrototype",
  version: "2023-01-01-preview",
})
@versionedDependency(Azure.ResourceManager.Versions.v1_0_Preview_1)
namespace Microsoft.OSConfigPrototype;

interface Operations extends Azure.ResourceManager.Operations {}

@doc("Device identity.")
model DeviceIdentity {
  @secret
  @visibility("create", "update")
  @doc("Identifier of the device.")
  deviceId?: string;
  
  @secret
  @visibility("create", "update")
  @doc("Identifier of the module.")
  moduleId?: string;

  @secret
  @visibility("create", "update")
  @doc("Connection string of the device.")
  connectionString?: string;
}

@doc("Device resource.")
model Device is TrackedResource<DeviceProperties> {
  @key("deviceName")
  @path
  @segment("devices")
  @pattern("^[a-zA-Z0-9-]{3,24}$")
  @doc("Name of the device.")
  name: string;
}

@doc("Device properties.")
model DeviceProperties {
  @extension("x-ms-secret", true)
  @visibility("create", "update")
  @doc("Identity of the device.")
  identity?: DeviceIdentity;

  @visibility("create", "update", "read")
  @doc("Desired state of the device.")
  desired?: DesiredProperties;

  @visibility("read")
  @doc("Reported state of the device.")
  reported?: ReportedProperties;

  @visibility("read")
  @doc("The status of the last operation.")
  provisioningState?: ProvisioningState;
}

@doc("Desired properties.")
model DesiredProperties {
  @visibility("create", "update", "read")
  @doc("Desired properties for CommandRunner.")
  commandRunner?: CommandRunner.Desired.Properties;

  @visibility("create", "update", "read")
  @doc("Desired properties for Firewall.")
  firewall?: Firewall.Desired.Properties;
  
  @visibility("create", "update", "read")
  @doc("Desired properties for HostName.")
  hostName?: HostName.Desired.Properties;
}

@doc("Reported properties.")
model ReportedProperties {
  @visibility("read")
  @doc("Reported properties for CommandRunner.")
  commandRunner?: CommandRunner.Reported.Properties;

  @visibility("read")
  @doc("Reported properties for DeviceInfo.")
  deviceInfo?: DeviceInfo.Reported.Properties;

  @visibility("read")
  @doc("Reported properties for Firewall.")
  firewall?: Firewall.Reported.Properties;

  @visibility("read")
  @doc("Reported properties for HostName.")
  hostName?: HostName.Reported.Properties;
}

@armResourceOperations
interface Devices extends ResourceOperations<Device, DeviceProperties> {
  delete is ArmResourceDeleteSync<Device>
}

@lroStatus
enum ProvisioningState {
  ...ResourceProvisioningState,
  Provisioning,
  Updating,
  Deleting,
  Accepted,
}
