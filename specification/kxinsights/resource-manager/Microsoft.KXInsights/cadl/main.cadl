import "@cadl-lang/rest";
import "@azure-tools/cadl-providerhub";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-azure-resource-manager";

@armNamespace
@serviceTitle("KX Insights Resource Provider")
@serviceVersion("2022-07-01-preview")
namespace Microsoft.KXInsights;

using Cadl.Http;
using Cadl.Rest;
using Azure.ResourceManager;

@doc("A KX Insights resource")
model KXInsightsResource is TrackedResource<KXInsightsProperties> {
  @pattern("^[a-zA-Z0-9-]{3,24}$")
  @key("KXInsightsName")
  @segment("KXInsights")
  @path
  @doc("The name of the KX Insights resource")
  name: string;
}

@secret
model PasswordString is string {}

@doc("Details of the KXInsights resource.")
model KXInsightsProperties {
  @visibility("write")
  @doc("The password to use for KeyCloak")
  @pattern("^(?=.*[a-z])(?=.*[A-Z])(?=.*[0-9])(?=.*[!@#\\$%\\^&\\*]).{12,320}$")
  keyCloakPassword: PasswordString;
}

@armResourceOperations
interface KXInsights extends ResourceOperations<KXInsightsResource, KXInsightsProperties> {
  @post
  @doc("Get the health of the cluster")
  @segment("healthcheck")
  op HealthCheck(
    ...ResourceInstanceParameters<KXInsightsResource>
  ): ArmResponse<string> | ErrorResponse;
}
