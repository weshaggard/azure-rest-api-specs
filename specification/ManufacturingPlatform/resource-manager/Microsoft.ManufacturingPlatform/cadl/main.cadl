import "@cadl-lang/rest";
import "@cadl-lang/versioning";
import "@azure-tools/cadl-providerhub";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-azure-resource-manager";

@armProviderNamespace
@service({
  title: "Microsoft.ManufacturingPlatform",
  version: "2023-02-01-preview",
})
@versionedDependency(Azure.ResourceManager.Versions.v1_0_Preview_1)
namespace Microsoft.ManufacturingPlatform;

using Cadl.Http;
using Cadl.Rest;
using Cadl.Versioning;
using Azure.Core;
using Azure.ResourceManager;

@doc("A ManufacturingPlatformProviderHub resource")
model MdsResource is TrackedResource<MdsResourceProperties> {
  @pattern("^[a-zA-Z0-9-]{3,24}$")
  @key("mdsResourceName")
  @segment("manufacturingDataServices")
  @doc("Name.")
  @path
  @visibility("read")
  name: string;
}

@doc("The status of the current operation.")
enum ProvisioningState {
  Succeeded,
  Failed,
  Canceled,
  Provisioning,
  Updating,
  Deleting,
  Accepted,
}


@doc("Details of the ManufacturingPlatform MdsResource.")
model MdsResourceProperties {
  @visibility("read")
  @doc("The status of the last operation.")
  provisioningState?: ProvisioningState;

  @doc("Mds Resource Version.")
  @visibility("read", "create", "update")
  version?: float32;

  @visibility("read", "create", "update")
  @doc("Enable Teams Notification.")
  enableTeamsNotification?: boolean;
    
  @visibility("read")
  @doc("Service Url")
  serviceUrl?: string;

  @visibility("read")
  @doc("Profile of AKS Created.")
  aksProfile?: AksProfile;

  @visibility("read")
  @doc("Profile of Storage Created.")
  storageProfile?: StorageProfile;

  @visibility("read")
  @doc("Profile of Database Created.")
  databaseProfile?: DatabaseProfile;

  @visibility("read")
  @doc("Profile of Adx Created.")
  adxProfile?: AdxProfile;

  @visibility("read")
  @doc("Profile of Adt Created.")
  adtProfile?: AdtProfile;

  @visibility("read")
  @doc("Profile of Akv Created.")
  akvProfile?: AkvProfile;

  @visibility("read")
  @doc("Profile of Azure Redis Created.")
  redisProfile?: RedisProfile;

  @visibility("read")
  @doc("Profile of Synapse Created.")
  synapseProfile?: SynapseProfile;

  @visibility("read")
  @doc("Profile of Monitoring Profile.")
  monitoringProfile?: MonitoringProfile;
}

@doc("The properties related to Aks Resource")
model AksProfile {
  
  @doc("Resource Id of AKS Resource")
  @visibility("read")
  id?: string;
}

@doc("The properties related to Storage Resource")
model StorageProfile {
  
  @doc("Resource Id of Storage Resource")
  @visibility("read")
  id?: string;
}

@doc("The properties related to Database Resource")
model DatabaseProfile {

  @doc("Resource Id of Cosmos Resource")
  @visibility("read")
  cosmosId?: string;
}

@doc("The properties related to Azure Data Factory (Adx) Resource")
model AdxProfile {
  
  @doc("Resource Id of Adx Resource")
  @visibility("read")
  id?: string;

  @doc("Uri of Adx Resource")
  @visibility("read")
  uri?: string;

  @doc("Data Ingestion Uri of Adx Resource")
  @visibility("read")
  dataIngestionUri?: string;

}

@doc("The properties related to Azure Data Twin (Adt) Resource")
model AdtProfile {
  
  @doc("Resource Id of Adt Resource")
  @visibility("read")
  id?: string;

  @doc("Host Name")
  @visibility("read")
  hostName?: string;

}

@doc("The properties related to Azure Key Vault (Akv) Resource")
model AkvProfile {
  
  @doc("Resource Id of Akv Resource")
  @visibility("read")
  id?: string;
    
}

@doc("The properties related to Azure Redis Cache Resource")
model RedisProfile {
  
  @doc("Resource Id of Azure Redis Cache Resource")
  @visibility("read")
  id?: string;
    
}

@doc("The properties related to Monitoring Resource")
model MonitoringProfile {
  
  @doc("Resource Id of Application Insights Resource")
  @visibility("read")
  id?: string;
    
}

@doc("The properties related to Synapse Resource")
model SynapseProfile {
  
  @doc("Resource Id of Synapse Resource")
  @visibility("read")
  id?: string;

  @doc("Synapse Development Endpoint")
  @visibility("read")
  devEndpoint?: string;
}

@armResourceOperations(MdsResource)
interface ManufacturingDataServices extends Azure.ResourceManager.Operations {
  get is ArmResourceRead<MdsResource>;
  createOrUpdate is ArmResourceCreateOrUpdateAsync<MdsResource>;
  update is ArmResourcePatchAsync<MdsResource, MdsResourceProperties>;
  delete is ArmResourceDeleteSync<MdsResource>;
  listByResourceGroup is ArmResourceListByParent<MdsResource>;
  listBySubscription is ArmListBySubscription<MdsResource>;
  listAvailableVersions is ArmResourceListActionSync<MdsResource, AvailableVersionListResult>;
}



// note this is a template for an empty body resource action, which will be added to the library at some point
#suppress "@azure-tools/cadl-azure-core/documentation-required" "This is a template"
#suppress "@azure-tools/cadl-azure-resource-manager/arm-resource-operation-outside-interface" "This is a template"
@armResourceAction(TResource)
@post
op ArmResourceListActionSync<
  TResource extends Azure.ResourceManager.Foundations.ArmResource,
  TResponse extends object,
  TBaseParameters = Azure.ResourceManager.Foundations.BaseParameters<TResource>
>(
  ...ResourceInstanceParameters<TResource, TBaseParameters>
): ArmResponse<TResponse> | ErrorResponse;


@doc("The list of available versions")
model AvailableVersionListResult {
  @doc("The list of versions")
  @OpenAPI.extension("x-ms-identifiers", [])
  versions: ApplicationVersion[];
}


@doc("Information about application versions")
model ApplicationVersion {
  @doc("Version")
  version: float32;

  @doc("Is Latest")
  isLatest: boolean;
}