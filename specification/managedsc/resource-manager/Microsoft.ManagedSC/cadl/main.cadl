import "@cadl-lang/rest";
import "@cadl-lang/versioning";
import "@azure-tools/cadl-autorest";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-azure-resource-manager";

using Cadl.Http;
using Cadl.Rest;
using Cadl.Versioning;
using Azure.Core;
using Azure.ResourceManager;

@armProviderNamespace
@service({title: "Microsoft.ManagedSC", version: "2023-02-01-preview"})
@versionedDependency(Azure.ResourceManager.Versions.v1_0_Preview_1)
namespace Microsoft.ManagedSC;

interface Operations extends Azure.ResourceManager.Operations {}

enum ProvisioningState {
  ...ResourceProvisioningState,
}

// All properties should be optional under TemplateParameterProperties
@doc("The properties to create new ManagedStorageClass")
model TemplateParameterProperties {
    @doc("The backing storageclass used to create new storageclass")
    backingStorageClassName?: string;

    @doc("[S3] AccessKeyID")
    accessKeyID?: string;

    @doc("[S3] S3 SecretAccessKey")
    secretAccessKey?: string;

    @doc("[S3] S3 Endpoint")
    endpoint?: string;

    @doc("[S3] S3 Region")
    region?: string;
}

@doc("The status of the ManagedStorageClass")
model ManagedStorageClassStatus {
    @doc("The status of the ManagedStorageClass")
    scProvisioningState?: string
}

@doc("The properties of ManagedStorageClass")
model ManagedStorageClassProperties {
    @doc("[Native] Volume can be expanded or not")
    allowVolumeExpansion?: boolean;

    @doc("[Native] Configured as default")
    default?: boolean;

    @doc("[Native] Additional mount options")
    mountOptions?: string[];

    @doc("[Native] Provisioner name")
    provisioner?: string;

    @doc("[Native] Binding mode of volumes: Immediate, WaitForFirstConsumer")
    volumeBindingMode?: string;

    @doc("The access mode: [RWO, RWX] or [RWO]")
    accessMode?: string[];

    @doc("Allow single data node failure")
    dataResilience?: boolean;

    @doc("Failover speed: NA, Slow, Fast")
    failoverSpeed?: string;

    @doc("Limitations")
    limitations?: string[];

    @doc("Performance tier")
    performance?: string;

    @doc("Selection priority when multiple storage classes meet the criteria. 0: Highest, -1: Never use")
    priority?: integer;

    @doc("Type of the ManagedStorageClass")
    storageClassType: string;

    @doc("Parameters to create new ManagedStorageClass")
    templateParameters?: TemplateParameterProperties;

    @doc("Actual Status of the ManagedStorageClass")
    status?: ManagedStorageClassStatus;

    @doc("Resource provision state")
    provisioningState?: ProvisioningState;
}

@doc("extended location")
model ExtendedLocation {
    @doc("extended location type: CustomLocation")
    type: string;

    @doc("extended location resource id")
    name: string;
}

@doc("A ManagedStorageClass")
model ManagedStorageClassResource is TrackedResource<ManagedStorageClassProperties> {
    @doc("Name of ManagedStorageClass")
    @key("managedstorageclassName")
    @segment("managedstorageclass")
    @path
    @visibility("read")
    @maxLength(64)
    name: string;

    // Extended location is not included in TrackedResource yet
    @doc("Custom location of ManagedStorageClass")
    extendedLocation: ExtendedLocation;
}

@armResourceOperations
interface ManagedStorageClass extends ResourceOperations<ManagedStorageClassResource, ManagedStorageClassProperties> {}
